/**
 *  Chapter 17 : Transactions
 *  https://academy.ligolang.org/js/chapter-transactions
 *
 *  1- Transfer the purchase_price to the vendor_contract
 *  2- Don't forget to return your operation.
 *
 *  cli test command
 *    - incorrect source:
 *      `ligo run dry-run tez-academy/transactions.jsligo -e main 'Pay({ item: 0 as nat, price: 10 as tez })' 'unit' --source "tz1KqTpEZ7Yob7QbPE4Hy4Wo8fHG8LhKxZSx" --amount 10`
 *    - incorrect amount:
 *      `ligo run dry-run tez-academy/transactions.jsligo -e main 'Pay({ item: 0 as nat, price: 10 as tez })' 'unit' --source "tz1TGu6TN5GSez2ndXXeDX6LgUDvLzPLqgYV" --amount 2`
 *    - pass:
 *      `ligo run dry-run tez-academy/transactions.jsligo -e main 'Pay({ item: 0 as nat, price: 10 as tez })' 'unit' --source "tz1TGu6TN5GSez2ndXXeDX6LgUDvLzPLqgYV" --amount 10`
 */

type command = {
  item: nat,
  price: tez
};

type mainAction =
| ["Pay", command]
| ["Buy"];

type storage = unit;

type return_ = [list<operation>, storage];

const purchase = ([_item, purchase_price, store]: [nat, tez, storage]): return_ => {
  const ship_address: address = ("tz1TGu6TN5GSez2ndXXeDX6LgUDvLzPLqgYV" as address);
  const vendor_address: address = ("tz1KqTpEZ7Yob7QbPE4Hy4Wo8fHG8LhKxZSx" as address);

  const _check_source: unit = assert_with_error ((Tezos.get_source() == ship_address), "Access denied");
  const _check_amount: unit = assert_with_error ((Tezos.get_amount() == purchase_price), "Incorrect amount");
  
  const vendor_contract: contract<unit> =
    match ((Tezos.get_contract_opt (vendor_address) as option<contract<unit>>), {
    Some: (c: contract<unit>) => c,
    None: () => (failwith("Contract not found.") as contract<unit>)
  });
    
  // Type your solution below
  const op: operation = Tezos.transaction (unit, purchase_price, vendor_contract);

  return [list([op]), store];
};

const main = (action: mainAction, store: storage): return_ =>
  match (action, {
    Pay: (cmd: command) => purchase(cmd.item, cmd.price, store),
    Buy: () => (failwith("not implemented") as return_)
  });
